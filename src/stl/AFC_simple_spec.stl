# We declare parameters with default values 
param tol=0.01, af_ref=14.7 
AF_abs_ok := abs(AF[t]- AFref[t]) <  tol*af_ref

# Slightly more complex definition of Air-Fuel Ratio being OK
AF_above_ref := AF[t]- AFref[t] >  tol*af_ref
AF_below_ref := AF[t]- AFref[t] < -tol*af_ref
AF_ok := not (AF_above_ref or AF_below_ref) 

# Top formula, using time parameters
param t_start=10, t_end=40
AF_alw_ok := alw_[t_start, t_end] (AF_ok)

# A => B type formula
param t_0=5
control_mode_check := (controller_mode[t] == 1)
AF_ok_controller := alw_[t_0, t_end] (control_mode_check => AF_ok)

# generic check, computed by postprocessing function 
AF_ok_check := alw_[t_0, t_end] (check[t]>0  => AF_ok)

# A => ev B formula
param t_stab=0.5, t_start=5
AF_overshoot_req := alw_[5, t_end] ( AF_above_ref => ev_[0, t_stab] ( AF_abs_ok )  ) 

# settling
param dt=0.1,epsi=1e-2

AF_settled := abs(AF[t+dt]-AF[t])<epsi*dt
AF_stable := alw_[0, 1] (AF_settled)
AF_will_be_stable := ev_[0,9] (AF_stable)
AF_alw_settle := alw_[10, 30] (AF_will_be_stable)


#phi_all_afc := ((AF_alw_ok) and (AF_ok_controller) and (AF_overshoot_req) and (AF_alw_settle))
phi_all_afc := ((AF_alw_ok) and (AF_ok_controller) and (AF_overshoot_req))
